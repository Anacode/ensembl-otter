#!/usr/local/bin/perl -w

### genome_canvas_test

use strict;
use GenomeCanvas;
use GenomeCanvas::Band::TilingPath;
use GenomeCanvas::Band::Plot;
use GenomeCanvas::Band::RepeatFeature;
use Bio::EnsEMBL::DBSQL::Obj;
use Hum::Submission 'prepare_statement'; # For Sanger sequence names
use Getopt::Long 'GetOptions';

{
    my $host = 'ensrv3';
    my $db   = 'chr20';
    my $user = 'ensro';
    my $fpc = 'chr20';
    my $static_type = 'SANGER';
    my $scale = 2000;
    my $n_tiles = 1;
    my $chr_name = '';
    my $chr_start = 0;
    my $chr_end = 0;
    my $font_size = 12;
    my $sanger_names = 0;
    GetOptions(
        'host=s'        => \$host,
        'db=s'          => \$db,
        'user=s'        => \$user,
        'fpc=s'         => \$fpc,
        'scale=s'       => \$scale,
        'type=s'        => \$static_type,
        'tiles=i'       => \$n_tiles,
        'chr=s'         => \$chr_name,
        'start=s'       => \$chr_start,
        'end=s'         => \$chr_end,
        'font_size=i'   => \$font_size,
        'sanger!'       => \$sanger_names,
        ) or die "Error processing command line options";

    if ($chr_start) {
        my $chr_start_num = parse_num($chr_start)
            or die "Bad chr_start '$chr_start'";
        $chr_start = $chr_start_num;
    }
    if ($chr_end) {
        my $chr_end_num = parse_num($chr_end)
            or die "Bad chr_end '$chr_end'";
        $chr_end = $chr_end_num;
    }
    
    my $mw = GenomeCanvas::MainWindow->new;
    $mw->title($fpc);
    my $gc = GenomeCanvas->new($mw);
    $gc->font_size($font_size);
    
    my $canvas = $gc->canvas;

    {
        my $db = Bio::EnsEMBL::DBSQL::Obj->new(
            -DBNAME => $db,
            -USER   => $user,
            -HOST   => $host,
            );

        $db->static_golden_path_type($static_type);
        my $stadp = $db->get_StaticGoldenPathAdaptor();

        my( $vc );
        if ($chr_start) {
            $chr_name ||= $fpc;
            print STDERR "Fetching region of chr '$chr_name' from $chr_start to $chr_end\n";
            $vc = $stadp->fetch_VirtualContig_by_chr_start_end(
                $chr_name,
                $chr_start,
                $chr_end,
                );
        } else {
            print STDERR "Fetching virtual contig for all of fpc contig '$fpc'\n";
            $vc = $stadp->fetch_VirtualContig_by_fpc_name($fpc);
        }
        
        my $tiling = GenomeCanvas::Band::TilingPath->new($vc);
        if ($sanger_names) {
            $tiling->name_morpher(\&get_sanger_name);
        } else {
            $tiling->name_morpher(\&trim_name);
        }
        $tiling->tiling_direction(-1);
        $gc->add_Band($tiling);
        
        #my $tiling2 = GenomeCanvas::Band::TilingPath->new($vc);
        #$tiling2->show_labels($labels);
        #$tiling2->tiling_direction(1);
        #$gc->add_Band($tiling2);

        my $repeat = GenomeCanvas::Band::RepeatFeature->new;
        $repeat->virtual_contig($vc);
        $repeat->repeat_classifier(\&repeat_class);
        $gc->add_Band($repeat);

        my $plot = GenomeCanvas::Band::Plot->new;
        $plot->virtual_contig($vc);
        $plot->range(0.3,0.7);
        $plot->x_major(0.2);
        $gc->add_Band($plot);

        print STDERR "Rendering canvas\n";
        $gc->residues_per_pixel($scale);
        $gc->render;
    }
    
    $gc->set_scroll_region;

    my $button_frame = $mw->Frame;
    $button_frame->pack(
        -side => 'top',
        );

    make_button($button_frame, 'Exit',
        #sub{ kill 'INT', $$ } );
        sub{ exit } );

    make_button($button_frame, 'Zoom in', sub{ $gc->zoom(2) } );
    make_button($button_frame, 'Zoom out', sub{ $gc->zoom(-2) } );
    make_button($button_frame, 'Redraw', sub{
        $gc->render;
        $gc->fix_window_min_max_sizes
        } );
    make_button($button_frame, 'Show outline', sub{
            foreach my $obj ($canvas->find('withtag', 'all')) {
                if ($canvas->type($obj) eq 'rectangle') {
                    $canvas->itemconfigure($obj, -outline => 'red');
                }
            }
        } );

    make_button($button_frame, 'PostScript',
        sub {
            my $page_width = 841;

            my @bbox = $canvas->cget('scrollregion');
            my $canvas_width  = $bbox[2] - $bbox[0];
            my $canvas_height = $bbox[3] - $bbox[1];
            my $tile_width = $canvas_width / $n_tiles;
            for (my $i = 0; $i < $n_tiles; $i++) {
                my( $file );
                if ($n_tiles == 1) {
                    $file = "$fpc.ps";
                } else {
                    my $n = ('01'..'99')[$i];
                    $file = "${fpc}_$n.ps";
                }
                $canvas->postscript(
                    -file => $file,
                    '-x' => $bbox[0] + ($i * $tile_width),
                    '-y' => $bbox[1],
                    -width  => $tile_width,
                    -height => $canvas_height,
                    -pageanchor => 'nw',
                    -pagex => 36,
                    -pagey => 36,
                    -pagewidth => $page_width - 72,
                    -rotate => 1,   # landscape
                    );
                warn "Printed to file '$file'"
            }
        });
    $gc->fix_window_min_max_sizes;
}


                            ###############
                            Tk::MainLoop();
                            ###############

sub parse_num {
    my( $string ) = @_;
    
    my ($int, $mod) = $string =~ /^([\d\.]+)(.?)/ or return;
    if ($mod) {
        $mod = lc $mod;
        if ($mod eq 'k') {
            $int *= 1e3;
        }
        elsif ($mod eq 'm') {
            $int *= 1e6
        }
        else {
            return;
        }
    }
    return $int;
}

sub get_sanger_name {
    my( $name ) = @_;
    
    my $acc = trim_name($name);
    return seq_name_from_acc($acc) || "Em:$acc";
}

{
    my( $sth );
    
    sub seq_name_from_acc {
        my( $acc ) = @_;
        
        $sth ||= prepare_statement(q{
            SELECT s.sequence_name
            FROM project_acc a
              , project_dump d
              , sequence s
            WHERE a.sanger_id = d.sanger_id
              AND d.seq_id = s.seq_id
              AND d.is_current = 'Y'
              AND a.accession = ?
            });
        $sth->execute($acc);
        my ($seq_name) = $sth->fetchrow;
        return $seq_name;
    }
}

sub trim_name {
    my( $name ) = @_;
    
    my ($trim) = $name =~ /^([^\.]+)/
        or confess("Can't get trimmed name from '$name'");
    return $trim;
}


sub make_button {
    my( $parent, $label, $command ) = @_;
    
    my $button = $parent->Button(
        -text => $label,
        -command => $command,
        );
    $button->pack(
        -side => 'left',
        );
    return $button;
}

sub add_contig {
    my( $canvas, $name, $length, $phase, $gap ) = @_;

    my $fill_color = $phase == 3 ? 'black' : 'grey';

    my ($right_most) = ($canvas->bbox('contig'))[2] || 0;

    my $size = $length / 2000;  # 1 pixel = 2 kbp
    my $x1 = $right_most - 1;
    $x1 += $gap * 7.5;
    my $rec = $canvas->createRectangle(
        $x1, 0, $x1 + $size, 8,
        -fill => $fill_color,
        -outline => undef,
        -tags => ['contig', $name],
        );

    my $label = $canvas->createText(
        $x1, -1,
        -text => $name,
        #-font => '-*-helvetica-medium-r-*-*-24-*-*-*-*-*-*-*',
        -font => ['helvetica', 12],
        -anchor => 'sw',
        -tags => ['contig_label', $name],
        );

    my @bbox = $canvas->bbox($name);

    my $sp = 5;
    expand_bbox(\@bbox, $sp);
    my $bkgd = $canvas->createRectangle(
        @bbox,
        -fill    => undef,
        -outline => undef,
        -tags => ['contig_bkgd', $name],
        );
    $canvas->lower($bkgd, $rec);
    nudge_into_free_space($canvas, $name);
}

sub expand_bbox {
    my( $bbox, $pad ) = @_;
    
    $bbox->[0] -= $pad;
    $bbox->[1] -= $pad;
    $bbox->[2] += $pad;
    $bbox->[3] += $pad;
}

sub nudge_into_free_space {
    my( $canvas, $ac ) = @_;
    
    my %self = map {$_, 1} $canvas->find('withtag', $ac);
    while (grep ! $self{$_}, $canvas->find('overlapping', $canvas->bbox($ac))) {
        $canvas->move($ac, 0, -5);
    }
}

{
    my( %repeat_class );

    sub repeat_class {
        my( $band, $repeat_name ) = @_;
        
        make_repeat_class($band) unless %repeat_class;
        return $repeat_class{$repeat_name};
    }
    
    ### Yuk!  I look through the headers of the RepeatMasker
    ### libraries for the classes of each repeat.
    sub make_repeat_class {
        my( $band ) = @_;
        
        my $dbobj = $band->virtual_contig->dbobj;
        my %is_ens_repeat = map {$_, 1} list_repeat_names($dbobj);
        my %valid_classes = map {$_, 1} $band->repeat_classes;

        local *LIB;
        my $dir = '/nfs/disk84/jgrg/work/repeatmasker/RepeatMasker08142000/Libraries';
        opendir LIB, $dir or die "Can't opendir '$dir' : $!";
        foreach my $file (map "$dir/$_", grep /\.lib$/, readdir LIB) {
            local *FA;
            open FA, $file or die "Can't read '$file' : $!";
            while (<FA>) {
                if (/^>(\S+)/) {
                    my ($rep, $class) = split /#/, $1, 2;
                    next unless $is_ens_repeat{$rep};
                    $class =~ s{/.+}{};
                    if (my $stored_class = $repeat_class{$rep}) {
                        warn "$rep: class '$class' doesn't match stored '$stored_class'"
                            if $stored_class ne $class;
                    } else {
                        $repeat_class{$rep} = $class;
                    }
                }
            }
            close FA;
        }
        close LIB;
    }
}

sub list_repeat_names {
    my( $dbobj ) = @_;
    
    my $sth = $dbobj->prepare(q{
        SELECT DISTINCT hid
        FROM repeat_feature
        });
    $sth->execute;
    my( @repeat );
    while (my ($r) = $sth->fetchrow) {
        push(@repeat, $r);
    }
    return @repeat;
}


__END__

=head1 NAME - genome_canvas_test

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

