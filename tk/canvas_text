#!/usr/local/bin/perl -w

### canvas_text

use strict;
use GenomeCanvas;

{
    my $mw = GenomeCanvas::MainWindow->new;
    $mw->title('Text test');
    $mw->bind(<Control-q>, sub {exit 0});
    $mw->bind(<Control-Q>, sub {exit 0});
    
    my $gc = GenomeCanvas->new($mw);
    
    my $canvas = $gc->canvas;
    my $rec = $canvas->createRectangle(1,1,500,200,
        -fill       => '#ffffff',
        -outline    => '#999999',
        );
    $canvas->Tk::bind('<Button-1>',     [ \&deal_with_button_1, Tk::Ev('@') ] );
    $canvas->Tk::bind('<BackSpace>',      \&backspace                         );
    $canvas->Tk::bind('<Left>',           \&go_left                           );
    $canvas->Tk::bind('<Right>',          \&go_right                          );
    $canvas->Tk::bind('<<alpha_num>>',  [ \&insert_character,   Tk::Ev('A') ] );
    
    $canvas->eventAdd('<<alpha_num>>', map "<KeyPress-$_>", ' ', "a".."z", "A".."Z", 0..9);

    my $size = 15;
    my $x = 10;
    my $y_begin = $x;
    for (my $i = 0; $i < 11; $i++) {
        my $y = $y_begin + ($i * $size + $i);
        my $row_num = $i + 1;
        my $text = "Text in row $row_num";
        make_editable_label($canvas, $x, $y, $text, $size);
    }
    
    $gc->fix_window_min_max_sizes;
    
    Tk::MainLoop();
}

sub make_editable_label {
    my( $canvas, $x, $y, $text, $size ) = @_;
    
    $size ||= 15;
    
    my $t = $canvas->createText(
        $x,$y,
        -anchor => 'nw',
        -text   => $text,
        -font   => ['lucidatypewriter', $size, 'normal'],
        -tags   => ['test text'],
        );
    #$canvas->bind($t, '<Button-1>',     \&select_text);
}

BEGIN {
    my $sel_tag = 'SelectedThing';
    my $selected = 0;

    sub deal_with_button_1 {
        my( $canvas, $xy ) = @_;

        my $obj = $canvas->find('withtag', 'current');
        unless ($obj) {
            deselect($canvas);
            return;
        }
        
        unless ($obj == $selected) {
            deselect($canvas);
        }

        my $type = $canvas->type($obj)
            or return;
        
        if ($type eq 'text') {
        
            # Position the icursor in the text
            my($x,$y) = canvas_x_y($canvas, $xy);
            my $pos = $canvas->index($obj, [$x, $y]) + 1;
            $canvas->icursor($obj, $pos);
            
            # Hightlight and focus if it isn't the
            # current object
            if ($obj != $selected) {
                $canvas->focus($obj);
                maintain_highlight_rectangle($canvas, $obj);
                $selected = $obj;
            }
        }
    }
    
    sub deselect {
        my( $canvas ) = @_;
        
        $canvas->delete($sel_tag);
        $canvas->focus("");
        $selected = 0;
    }
    
    sub maintain_highlight_rectangle {
        my( $canvas, $obj ) = @_;
        
        $canvas->delete($sel_tag);
        
        my @bbox = $canvas->bbox($obj);
        $bbox[0] -= 1;
        $bbox[1] -= 1;
        $bbox[2] += 1;
        $bbox[3] += 1;
        my $rec = $canvas->createRectangle(
            @bbox,
            -fill       => '#ffff00',
            -outline    => undef,
            -tags       => [$sel_tag],
            );
        $canvas->lower($rec, $obj);
    }
}

sub canvas_x_y {
    my( $canvas, $xy ) = @_;
    
    my ($x,$y) = $xy =~ /^\@(\d+),(\d+)/
        or die "Wierd xy: '$xy'";
    $x = $canvas->canvasx($x);
    $y = $canvas->canvasy($y);
    
    return($x, $y);
}

sub backspace {
    my( $canvas ) = @_;

    my $text = $canvas->focus or return;

    my $pos = $canvas->index($text, 'insert');
    $canvas->dchars($text, $pos - 1);
    maintain_highlight_rectangle($canvas, $text);
}

sub go_left {
    my( $canvas ) = @_;

    my $text = $canvas->focus or return;

    my $pos = $canvas->index($text, 'insert');
    $canvas->icursor($text, $pos - 1);
}

sub go_right {
    my( $canvas ) = @_;

    my $text = $canvas->focus or return;

    my $pos = $canvas->index($text, 'insert');
    $canvas->icursor($text, $pos + 1);
}

    
sub insert_character {
    my( $canvas, $char ) = @_;

    return unless $char =~ /^[a-zA-Z0-9 ]$/;

    my $text = $canvas->focus or return;
    $canvas->insert($text, 'insert', $char);
    maintain_highlight_rectangle($canvas, $text);
}

__END__

=head1 NAME - canvas_text

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

