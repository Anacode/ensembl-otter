#!/usr/local/bin/perl5.6.1 -w

### save_xml

use strict;
use LWP;
use URI::Escape qw{ uri_escape };
use Bio::Otter::Lace::Defaults;

{
    my( $author, $email, $dataset );
    my $usage = sub{ exec('perldoc', $0) };
    Bio::Otter::Lace::Defaults::do_getopt(
        'dataset=s' => \$dataset,
        'author=s'  => \$author,
        'email=s'   => \$email,
        'h|help!'   => $usage,
        );
    $usage->() unless $dataset and $author and $email;
    
    my( $xml );
    {
        local $/ = undef;
        $xml = <>;
    }
    
    my $cl = Bio::Otter::Lace::Defaults::make_Client();
    
    # Save to server with POST
    my $url = $cl->url_root . '/write_region';
    my $request = HTTP::Request->new;
    $request->method('POST');
    $request->uri($url);
    $request->content(
        join('&',
            'author='   . uri_escape($author),
            'email='    . uri_escape($email),
            'dataset='  . uri_escape($dataset),
            'data='     . uri_escape($xml),
            'unlock=true',
            )
        );
    
    my $content = $cl->get_UserAgent->request($request)->content;
    $cl->_check_for_error(\$content);
}

__END__

=head1 NAME - save_xml

  save_xml -dataset <DATASET> -author <NAME> -email <ADDRESS>

=head1 AUTHOR

James Gilbert B<email> jgrg@sanger.ac.uk

