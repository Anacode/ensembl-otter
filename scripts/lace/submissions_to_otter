#!/usr/local/bin/perl -w

=head1 NAME
    
submssion_to_otter

=head1  SYNOPSIS 
    
submission_to_otter [-host] [-port] [-user] [-dbname] [-dbpass]

=head1 DESCRIPTION         
    
checks through the contigs in an otter databases and if no sequence notes are present, it copies them from the submissions database

=cut 
 

use strict ;
use Carp;
use Hum::Submission qw{sub_db prepare_statement};
use DBI;
use Getopt::Long 'GetOptions';

my $otter_db;

{
    my $host = 'localhost'; 
    my $port = 3306; 
    my $count = 0;
    my $domain = 'sanger.ac.uk';
    my( $dbname, $dbpass, $user );

    GetOptions(
        'password=s'=> \$dbpass,
        'dbname=s'  => \$dbname,
        'port=i'    => \$port,
        'host=s'    => \$host,
        'user=s'    => \$user,
        'domain=s'  => \$domain,
         ) ; 

    $otter_db = DBI->connect("DBI:mysql:host=$host;port=$port;database=$dbname",
                                   $user, $dbpass, {RaiseError => 1, PrintError => 0})
                    or die "Can't connect to submissions database as '$user' ",
                    DBI::errstr();
    if ($domain) {
        $domain = "\@$domain";
    }
    
    my $fetch_notes_sanger = prepare_statement(q{
        SELECT r.annotator_uname
          , r.review_comment
          , r.review_time
        FROM project_acc pa
          , project_dump d
          , sequence s
          , ana_sequence a
        STRAIGHT_JOIN ana_sequence_review r
        WHERE pa.sanger_id = d.sanger_id
          AND d.seq_id = s.seq_id
          AND s.seq_id = a.seq_id
          AND a.ana_seq_id = r.ana_seq_id
          AND d.is_current = 'Y'
          AND a.is_current = 'Y'
          AND pa.accession = ?
          AND s.sequence_version = ?
        ORDER BY r.review_time DESC
        });
    
    my $fetch_notes_external = prepare_statement(q{
        SELECT r.annotator_uname
          , r.review_comment
          , r.review_time
        FROM sequence s
          , ana_sequence a
        STRAIGHT_JOIN ana_sequence_review r
        WHERE s.seq_id = a.seq_id
          AND a.ana_seq_id = r.ana_seq_id
          AND a.is_current = 'Y'
          AND s.sequence_name = ?
          AND s.sequence_version = ?
        ORDER BY r.review_time DESC
        });

    my $insert_note = $otter_db->prepare(q{
        INSERT INTO sequence_note( author_id
              , contig_id
              , is_current
              , note
              , note_time)
        VALUES(?,?,?,?,?)
        });

    my $author_id_hash = get_author_id_hash($otter_db);

    # query otter database for each of the contigs that have no comment
    my $otter_sth = $otter_db->prepare(q{
        SELECT cl.embl_acc
          , cl.embl_version
          , cg.contig_id
        FROM clone cl
          , contig cg
        LEFT JOIN sequence_note sn
          ON sn.contig_id = cg.contig_id
        WHERE cg.clone_id = cl.clone_id
          AND sn.note IS NULL
        }) ;
    $otter_sth->execute;

    while (my ($accession, $version, $contig_id) = $otter_sth->fetchrow){
        foreach my $sth ($fetch_notes_sanger, $fetch_notes_external) {
            $sth->execute($accession, $version);
            my ($username, $comment, $time);
            $sth->bind_columns( \$username, \$comment, \$time );

            # set initaially to 'Y' for the first note (most recent) copied over, then set to 'N'
            my $is_current = 'Y';
            while ($sth->fetch) {
                next unless $comment;
                my $author_id = $author_id_hash->{$username} ||= insert_new_author($username, $domain);
                # insert the note 
                $insert_note->execute($author_id, $contig_id, $is_current, $comment, $time);
                $count++;
                $is_current = 'N';
            }
            last if $sth->rows;
        }
    }

    print STDERR "\n", $count ." sequence notes copied over\n";

}

sub get_author_id_hash {
    my( $db ) = @_;
    
    my $sth = $db->prepare(q{
        SELECT author_id
          , author_name
        FROM author
        });
    $sth->execute;
    
    my $authors = {};
    while (my ($id, $name) = $sth->fetchrow) {
        $authors->{$name} = $id;
    }
    return $authors;
}

sub insert_new_author {
    my( $username, $domain ) = @_;

    my $sth = $otter_db->prepare(qq{ 
        INSERT INTO author (author_name
              , author_email)
        VALUES (?,?)
        });
    $sth->execute($username, "$username$domain");
    my $id = $sth->{'mysql_insertid'}
        or confess "No insert_id from author insert";

    return $id;
}


=head1 AUTHOR

Colin Kingswood<email> ck2@sanger.ac.uk

