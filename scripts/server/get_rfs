#!/usr/local/bin/perl5.8.0 -w

my $enshead = $ENV{ENSHEAD}; # is set by the server for any GET request

use strict;
use OtterDefs;
use CGI;
use Bio::Otter::ServerSide (':all');
use Bio::Otter::Lace::PipelineDB;
use Bio::EnsEMBL::DBSQL::DBAdaptor;
use Bio::Otter::DBSQL::DBAdaptor;
use Bio::Otter::Lace::ViaText ('%OrderOfOptions');

$| = 1;

my $cgi = new CGI;
set_nph($cgi);
my %cgi_args = $cgi->Vars;

my $analysis = $cgi_args{analysis};
my $odb = get_DBAdaptor_from_CGI_species($cgi, $OTTER_SPECIES, $enshead);

my $pipekey = $enshead
    ? 'pipeline_db_head'
    : 'pipeline_db';

    # Getting (to) the pipeline adapter:
my @pipe_all = @{ $odb->get_MetaContainer()->list_value_by_key($pipekey) };
if(!@pipe_all) {
    die "This database does not have an associated '$pipekey' in the meta table ";
}
my $pipe_lastval = pop @pipe_all; # we prefer the one added the latest
my $pdb = Bio::EnsEMBL::DBSQL::DBAdaptor->new( eval $pipe_lastval );

my $pipeline_slice;

if($enshead) {
    $pipeline_slice = $pdb->get_SliceAdaptor()->fetch_by_region(
        'chromosome',
        $cgi_args{chr},
        $cgi_args{chrstart},
        $cgi_args{chrend},
        undef,              # strand
        'Otter',            # version
    );
} else {
    $pdb->assembly_type($odb->assembly_type());

    $pipeline_slice = $pdb->get_SliceAdaptor()->fetch_by_chr_start_end(
        $cgi_args{chr},
        $cgi_args{chrstart},
        $cgi_args{chrend},
    );
}

my @rf_optnames = @{ $OrderOfOptions{RepeatFeature} };
my @rc_optnames = @{ $OrderOfOptions{RepeatConsensus} };

    # Fetch the features:
my $rf_adaptor = $pdb->get_RepeatFeatureAdaptor();
my $rfs = $rf_adaptor->fetch_all_by_Slice($pipeline_slice, $analysis);

print STDERR "Total of ".scalar(@$rfs)." $analysis repeat features found\n";

    # Stringify only the simple fields:
my %rc_seen = (); # collect the seen repeat consensus ids here
foreach my $rf (@$rfs) {
    my $rc = $rf->repeat_consensus(); # object
    my $rc_id = $rc->dbID();

    if(!exists($rc_seen{$rc_id})) { # a new one

            # output a repeat consensus line
        my @rc_optvalues = ('RepeatConsensus');
        for my $opt (@rc_optnames) {
            push @rc_optvalues, $rc->$opt();
        }
        print join("\t", @rc_optvalues)."\n";

        $rc_seen{$rc_id} = 1;
    }
    
        # output a repeat feature line
    my @rf_optvalues = ('RepeatFeature');
    for my $opt (@rf_optnames) {
        push @rf_optvalues, $rf->$opt();
    }
    push @rf_optvalues, $rc_id;

    print join("\t", @rf_optvalues)."\n";
}

